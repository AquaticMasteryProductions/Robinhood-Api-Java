plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

group = 'com.ampro'
version = '0.8.2'

dependencies {
    //Running
    compile group: 'io.github.openunirest', name: 'unirest-java', version: '2.2.04' //OpenUnirest
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    //Testing
    testCompile 'junit:junit:4.12'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.18.0'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'robinhood-api'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Robinhood API'
                description = 'A Java wrapper for the Robinhood investing service\'s API'
                url = 'https://github.com/AquaticMasteryProductions/Robinhood-Api-Java'
                licenses { license { name = 'MIT License' } }
                developers {
                    developer {
                        id = 'JonoAugustine'
                        name = 'Jonathan Augustine'
                        email = 'Aqua@jhu.edu'
                    }
                }
            }
        }
    }
}
